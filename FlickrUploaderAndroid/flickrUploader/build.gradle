import org.ajoberstar.grgit.Grgit

buildscript {
    repositories {
        mavenCentral()
        maven { url 'https://maven.fabric.io/public' }
        maven { url "https://plugins.gradle.org/m2/" }
    }

    dependencies {
        // This version is *supposed to* be dynamic according to the Fabric docs
        //noinspection GradleDynamicVersion
        classpath 'io.fabric.tools:gradle:1.+'

        classpath 'com.neenbedankt.gradle.plugins:android-apt:1.8'
        classpath "org.ajoberstar:gradle-git:1.5.1"
    }
}
apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
apply plugin: 'android-apt'
apply plugin: "org.ajoberstar.grgit"

repositories {
    maven { url 'https://maven.fabric.io/public' }
}

apt {
    arguments {
        androidManifestFile variant.outputs[0].processResources.manifestFile
        // if you have multiple outputs (when using splits), you may want to have other index than 0

        resourcePackageName 'com.rafali.flickruploader2'

        // If you're using Android NBS flavors you should use the following line instead of hard-coded packageName
        // resourcePackageName android.defaultConfig.packageName

        // You can set optional annotation processing options here, like these commented options:
        // logLevel 'INFO'
        // logFile '/var/log/aa.log'
    }
}

ext {
    git = Grgit.open()
    gitVersionCode = git.tag.list().size()
    gitVersionName = "${git.describe()}"
}

android {
    compileSdkVersion 23
    buildToolsVersion '25.0.2'

    defaultConfig {
        applicationId "com.gmail.walles.johan.flickruploader2"
        minSdkVersion 21
        // Upping the targetSdkVersion from 22 requires implementing the new-for-API-23 permissions
        // scheme.
        //noinspection OldTargetApi
        targetSdkVersion 22
        versionCode gitVersionCode
        versionName gitVersionName
    }

    // See: http://stackoverflow.com/a/21020469/473672
    signingConfigs {
        release {
            storeFile file(RELEASE_STORE_FILE)
            storePassword RELEASE_STORE_PASSWORD
            keyAlias RELEASE_KEY_ALIAS
            keyPassword RELEASE_KEY_PASSWORD
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
            signingConfig signingConfigs.release
        }
    }

    lintOptions {
        abortOnError true
        warningsAsErrors true
        checkAllWarnings true

        lintConfig file("${project.rootDir}/lint.xml")

        textOutput "stdout"
        textReport true

        htmlReport true
    }
}

dependencies {
    apt "org.androidannotations:androidannotations:3.3.2"
    compile 'org.androidannotations:androidannotations-api:3.3.2'
    compile project(':sprinkles')
    compile 'com.android.support:appcompat-v7:23.1.0'
    compile('com.github.chrisbanes.bitmapcache:library:2.3') {
        exclude module: 'support-v4'
    }
    compile 'com.jakewharton:disklrucache:2.0.2'
    compile files('libs/flickrj-android-cleaned-2.0.6.jar')
    compile 'com.google.guava:guava:18.0'
    compile 'com.github.tony19:logback-android-classic:1.1.1-6'
    compile 'org.slf4j:slf4j-api:1.7.25'
    compile('com.crashlytics.sdk.android:crashlytics:2.9.5@aar') {
        transitive = true
    }
    compile('com.crashlytics.sdk.android:answers:1.4.3@aar') {
        transitive = true
    }
}
